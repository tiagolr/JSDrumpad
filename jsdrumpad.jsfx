desc:JSDrumpad

slider1:mix=0<-1,1,.01>Mix (Osc - Noise)
slider2:osc_freq=440<20,10000,1:log>Frequency (Hz)
slider3:osc_wave=1<0,3,1{Sine, Triangle, Saw, Square}>Wave
slider4:vol=-12<-62,0,.01>Volume
slider5:pan=0<-1,1,.01>Pan
slider6:distortion=0<0,1,.01>Distortion

slider7:noise_stereo=0<0,1,1{No, Yes}>Stereo noise

@init

osc_cycle = 0;

function wrap(number) (
  number <= 1 ? number : 0;
);

function sine_wave(cycle) (
  sin(cycle * 2 * $pi);
);

function tri_wave(cycle) (
  (cycle > 0.5 ? 4 * cycle - 2 : -4 * cycle + 2) - 1;
);

function saw_wave(cycle) (
  cycle * -2 + 1;
);

function square_wave(cycle) (
  cycle < 0.5 ? -1 : 1;
);

function make_noise(cycle) (
  rand(2) - 1;
);

function make_wave(cycle) (
  osc_wave == 0 ? sine_wave(cycle)
    : osc_wave == 1 ? tri_wave(cycle)
    : osc_wave == 2 ? saw_wave(cycle)
      : square_wave(cycle)
);

function db2gain(db) ( 
  10^(db / 20);
);

@slider

gain = db2gain(vol);
drive = 2*min(distortion, 0.999)/(1-min(distortion, 0.999))

@sample

osc_cycle = wrap(osc_cycle + osc_freq / srate);
wave_l = make_wave(osc_cycle);
wave_r = wave_l;
noise_l = make_noise();
noise_r = noise_stereo ? make_noise() : noise_l;

mix < 0 ? (
  noise_l *= 1 + mix;
  noise_r *= 1 + mix;
);
mix > 0 ? (
  wave_l *= 1 - mix;
  wave_r *= 1 - mix;
);
pan < 0 ? (
  noise_r *= 1 + pan;
  wave_r *= 1 + pan;
);
pan > 0 ? (
  noise_l *= 1 - pan;
  wave_l *= 1 - pan;
);

spl0 = (noise_l + wave_l);
spl1 = (noise_r + wave_r);

distortion > 0 ? (
  spl0 = (1+drive)*spl0/(1+drive*abs(spl0));
  spl1 = (1+drive)*spl1/(1+drive*abs(spl1));
);

spl0 *= gain;
spl1 *= gain;
