desc:dp.gfxlib.jsfx-inc

@init
function deg2rad (deg) (
  deg * $pi / 180;
);

function set_color(color) (
  gfx_r = (color & 0xFF0000) / 0xFF0000;
  gfx_g = (color & 0x00FF00) / 0x00FF00;
  gfx_b = (color & 0x0000FF) / 0x0000FF;
);

function log_scale (value, max, min) (
    minP = min;
    maxP = max;

    minV = log(min);
    maxV = log(max);

    scale = (maxV - minV) / (maxP - minP);
    exp(minV + scale * (value - minP));
);

function inverse_log_scale (lg, max, min) (
    minP = min;
    maxP = max;

    minV = log(min);
    maxV = log(max);

    scale = (maxV - minV) / (maxP - minP);
    (log(lg) - minV) / scale + minP;
);

function mouse_in_rect (x, y, w ,h) (
  mouse.x >= x && mouse.x <= x + w && mouse.y >= y && mouse.y <= y + h;
);

function draw_knob(x, y, nslider, label, default, _min, _max, is_log, is_sym, val_label) (
  set_color(0x282D32);
  rad130 = deg2rad(130);
  gfx_arc(x+20, y+20, 20, -rad130, rad130, 1);
  gfx_arc(x+20, y+20, 19.5, -rad130, rad130, 1);
  gfx_arc(x+20, y+20, 19, -rad130, rad130, 1);
  gfx_arc(x+20, y+20, 18.5, -rad130, rad130, 1);

  slider_val = slider(nslider);
  is_log ? (
    slider_val = inverse_log_scale(slider_val, _max, _min);
  );

  // linear map value from min/max to -130deg +130deg
  scale = (130 - -130) / (_max-_min);
  _offset = (-_min * (130 - -130)) / (_max - _min) + -130;
  slider_deg = slider_val * scale + _offset;
  slider_rad = deg2rad(slider_deg);

  gfx_circle(x+20, y+20, 15, 1, 1);
  set_color(0x5DBEE4);
  gfx_circle(x+20-sin(-slider_rad)*10, y+20-cos(-slider_rad)*10, 3, 1);

  gfx_arc(x+20, y+20, 20, is_sym ? 0 : -rad130, slider_rad, 1);
  gfx_arc(x+20, y+20, 19.5, is_sym ? 0 : -rad130, slider_rad, 1);
  gfx_arc(x+20, y+20, 19, is_sym ? 0 : -rad130, slider_rad, 1);
  gfx_arc(x+20, y+20, 18.5, is_sym ? 0 : -rad130, slider_rad, 1);

  set_color(0xFFFFFF);
  gfx_x = x - 40;
  gfx_y = y + 20 * 2 + 5;
  selknob_nslider == nslider ? (
    gfx_drawstr(val_label, 1, x+40+20*2, y+100);
  ) : (
    gfx_drawstr(label, 1, x+40+20*2, y+100);
  );

  mouse_in_rect(x, y, 40, 40) ? (
    mouse.double_click ? slider(nslider) = default;
    mouse.left_click ? (
      selknob_nslider = nslider;
      selknob_min = _min;
      selknob_max = _max;
      selknob_is_log = is_log;
    );
  );
);